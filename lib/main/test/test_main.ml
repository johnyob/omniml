open! Core
open! Grace
open Omniml_main

let () =
  Async.Log.Global.For_testing.use_test_output ();
  Omniml_error.For_testing.use_expect_test_config ()
;;

let type_check_and_print
      ?(dump_ast = false)
      ?(dump_constraint = false)
      ?(with_stdlib = true)
      ?(defaulting = Options.Defaulting.default)
      ?(log_level = `Info)
      str
  =
  Async.Log.Global.set_level log_level;
  let source = Omniml_source.For_testing.expect_test_source str in
  type_check_and_print
    ~source
    ~dump_ast
    ~dump_constraint
    ~with_stdlib
    ~defaulting
    (Lexing.from_string ~with_positions:true str)
;;

let include_ref =
  {|
    type 'a ref;;
    type 'a ref_repr = { contents : 'a };;

    external create_ref : 'a. 'a -> 'a ref;;
    external get_ref : 'a. 'a ref -> 'a;;
    external set_ref : 'a. 'a ref -> 'a -> unit;;
    external ref_repr : 'a. 'a ref -> 'a ref_repr;;
  |}
;;

let include_fix = "external fix : 'a 'b. (('a -> 'b) -> 'a -> 'b) -> 'a -> 'b;;"

let include_list =
  {|
    type 'a list =
      | Nil
      | Cons of 'a * 'a list
    ;;
  |}
;;

let include_option =
  {|
    type 'a option =
      | None
      | Some of 'a
    ;;
  |}
;;

let%expect_test "" =
  let str =
    include_fix
    ^ {|
      let power = fix (fun power x n ->
          if n = 0
            then 1
            else x * power x (n - 1)
        )
      ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    include_fix
    ^ {|
      external mod : int -> int -> int;;

      let even = fun n -> mod n 2 = 0;;

      let power = fix (fun power x n ->
          if n = 1
            then x
            else if even n
              then power (x * x)  (n / 2)
              else x * power (x * x) (n / 2)
        )
      ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    include_fix
    ^ {|
      let sum =
        fix (fun sum n ->
          if n = 0 then 0
          else n + sum (n - 1))
      ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    include_fix
    ^ {|
      let sum = fun n ->
        let loop = fix (fun loop n acc ->
          if n = 0 then acc
          else loop (n - 1) (n + acc))
        in loop n
      ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    include_fix
    ^ include_list
    ^ {|
      let mem = fix (fun mem t x equal ->
        match t with
        ( Nil -> false
        | Cons (y, t) ->
          if equal x y then true
          else mem t x equal
        ))
      ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    include_fix
    ^ include_list
    ^ {|
      let zip =
        fix (fun zip t1 t2 ->
          match (t1, t2) with
          ( (Cons (x1, t1), Cons (x2, t2)) ->
              Cons ((x1, x2), zip t1 t2)
          | _ -> Nil
          ))
      ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    include_fix
    ^ include_list
    ^ {|
      let unzip =
        fix (fun unzip t ->
          match t with
          ( Nil -> (Nil, Nil)
          | Cons ((x1, x2), t) ->
            let t1t2 = unzip t in
            match t1t2 with (
              (t1, t2) -> (Cons (x1, t1), Cons (x2, t2))
            )
          )
        )
      ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    include_fix
    ^ include_list
    ^ {|
      external raise_no_more_coins : 'a. unit -> 'a;;

      let change =
        fix (fun change till amt ->
          match (till, amt) with
          ( (_, 0) -> Nil
          | (Nil, _) -> raise_no_more_coins ()
          | (Cons (c, till), amt) ->
            if amt < c then change till amt
            else Cons (c, change (Cons (c, till)) (amt - c) )
          )
        )
      ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    include_fix
    ^ include_list
    ^ {|
      external append : 'a. 'a list -> 'a list -> 'a list;;

      let change =
        fix (fun change till amt ->
          match (till, amt) with
          ( (_, 0) -> Cons (Nil, Nil)
          | (Nil, _) -> Nil
          | (Cons (c, till), amt) ->
            if amt < c then change till amt
            else
              let loop = fix (fun loop t ->
                  match t with
                  ( Nil -> Nil
                  | Cons (cs, css) -> Cons (Cons (c, cs), loop css)
                  )
                )
              in
                append
                  (loop (change (Cons (c, till)) (amt - c)))
                  (change till amt)
          )
        )
      ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    include_fix
    ^ include_list
    ^ {|
      let change =
        fix (fun change till amt ->
          let loop = fix
            (fun loop till amt chg chgs ->
              match (till, amt) with
              ( (_, 0) -> Cons (chg, chgs)
              | (Nil, _) -> chgs
              | (Cons (c, till), amt) ->
                  if amt < 0 then chgs
                  else
                    loop (Cons (c, till)) (amt - c) (Cons (c, chg)) (loop till amt chg chgs)
              )
            )
          in loop till amt Nil Nil
        )
      ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    {|
      type vehicle =
        | Bike
        | Motorbike
        | Car
        | Lorry
      ;;

      let m = Motorbike;;

      let wheels =
        fun t ->
          match t with
          ( Bike -> 2
          | Motorbike -> 2
          | Car -> 4
          | Lorry -> 18
          )
      ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    {|
      type vehicle =
        | Bike
        | Motorbike of int (* engine size in CCs *)
        | Car of bool (* true if a Reliant Robin *)
        | Lorry of int (* number of wheels *)
      ;;

      let wheels =
        fun t ->
          match t with
          ( Bike -> 2
          | Motorbike _ -> 2
          | Car is_robin -> if is_robin then 3 else 4
          | Lorry w -> w
          )
      ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    include_option
    ^ {|
      let x = Some 1;;

      let y = None;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    include_fix
    ^ include_list
    ^ {|
      external raise_no_change : 'a. int -> 'a;;
      external try_with_no_change : 'a. (unit -> 'a) -> (int -> 'a) -> 'a;;

      let change =
        fix (fun change till amt ->
          match (till, amt) with
          ( (_, 0) -> Nil
          | (Nil, amt) -> raise_no_change amt
          | (Cons (c, till), amt) ->
              if amt < c
                then raise_no_change amt
                else try_with_no_change
                      (fun () -> Cons (c, change (Cons (c, till)) (amt - c)))
                      (fun _ -> change till amt)
          )
        )
      ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    {|
      type shape =
        | Null
        | Circle of int (* radius *)
        | Join of shape * shape
      ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    include_fix
    ^ include_list
    ^ {|
      type 'a tree =
        | Lf
        | Br of 'a tree * 'a * 'a tree
      ;;

      external append : 'a. 'a list -> 'a list -> 'a list;;

      let pre_order =
        fix (fun pre_order t ->
          match t with
          ( Lf -> Nil
          | Br (l, x, r) ->
            append (Cons (x, Nil))
              (append (pre_order l) (pre_order r))
          )
        )
      ;;

      let in_order =
        fix (fun in_order t ->
          match t with
          ( Lf -> Nil
          | Br (l, x, r) ->
            append (pre_order l)
              (append (Cons (x, Nil)) (pre_order r))
          )
        )
      ;;

      let post_order =
        fix (fun post_order t ->
          match t with
          ( Lf -> Nil
          | Br (l, x, r) ->
            append (post_order l)
              ( append (post_order r) (Cons (x, Nil)) )
          )
        )
      ;;

      let in_order = fun t ->
        let loop =
          fix (fun loop t acc ->
            match t with
            ( Lf -> acc
            | Br (l, x, r) ->
              loop l (Cons (x, loop r acc))
            )
          )
        in loop t
      ;;

      let pre_order = fun t ->
        let loop =
          fix (fun loop t acc ->
            match t with
            ( Lf -> acc
            | Br (l, x, r) ->
              Cons (x, loop l (loop r acc))
            )
          )
        in loop t
      ;;

      let post_order = fun t ->
        let loop =
          fix (fun loop t acc ->
            match t with
            ( Lf -> acc
            | Br (l, x, r) ->
              loop l (loop r (Cons (x, acc)))
            )
          )
        in loop t
      ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    include_list
    ^ {|
      let a1 =
        Cons (fun n -> n * 2, Cons (fun n -> n * 3, Cons (fun n -> n + 1, Nil)))
      ;;

      let a2 =
        fun n -> n * 2
      ;;

      let a3 =
        (fun n -> n * 2) 17
      ;;

      let double = fun n -> n * 2;;

      let a4 =
        fun x -> match x with (0 -> true | _ -> false)
      ;;

      let is_zero =
        fun x -> match x with (0 -> true | _ -> false)
      ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    include_fix
    ^ include_list
    ^ {|
      external map : 'a 'b. ('a -> 'b) -> 'a list -> 'b list;;
      external hd : 'a. 'a list -> 'a;;
      external tl : 'a. 'a list -> 'a list;;

      let transpose =
        fix (fun transpose t ->
          match t with
          ( Cons (Nil, _) -> Nil
          | rows ->
            Cons (map hd rows, transpose (map tl rows))
          )
        )
      ;;

      let dot_product =
        fix (fun dot_product xs ys ->
          match (xs, ys) with
          ( (Nil, Nil) -> 0
          | (Cons (x, xs), Cons (y, ys)) ->
              (x * y) + dot_product xs ys
          )
        )
      ;;


      let product =
        fun a b ->
          let c = transpose b in
          map (fun rows -> map (dot_product rows) c) a
      ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    include_fix
    ^ {|
      type 'a tree =
        | Lf
        | Br of 'a tree * 'a * 'a tree
      ;;

      let cons =
        fix (fun cons t x ->
          match t with
          ( Lf -> Br (Lf, x, Lf)
          | Br (l, y, r) ->
              Br (cons l y, x, r)
          )
        )
      ;;

      external invalid_arg : 'a. unit -> 'a;;

      let uncons =
        fix (fun uncons t ->
          match t with
          ( Lf -> invalid_arg ()
          | Br (Lf, x, Lf) -> (x, Lf)
          | Br (l, x, r) ->
            match uncons l with (
              (y, l') -> (x, Br (r, x, l'))
            )
          )
        )
      ;;

      let hd = fun t ->
        match uncons t with ((x, _) -> x)
      ;;

      let tl = fun t ->
        match uncons t with ((_, t) -> t)
      ;;

      external mod : int -> int -> int;;

      let even = fun n -> mod n 2 = 0;;

      let nth =
        fix (fun nth t n ->
          match (t, n) with
          ( (Lf, _) -> invalid_arg ()
          | (Br (_, x, _), 0) -> x
          | (Br (l, x, r), n) ->
              if even n then nth r (n / 2)
              else nth l (n / 2)
          )
        )
      ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    include_fix
    ^ include_list
    ^ {|
      type 'a tree =
        | Lf
        | Br of 'a tree * 'a * 'a tree
      ;;

      external raise_empty : 'a. unit -> 'a;;

      type 'a queue = Q of 'a list * 'a list;;
      let empty = Q (Nil, Nil);;

      let is_empty = fun q ->
        match q with
        ( Q (Nil, Nil) -> true
        | _ -> false)
      ;;

      external rev : 'a. 'a list -> 'a list;;

      let norm = fun q ->
        match q with
        ( Q (Nil, ys) -> Q (rev ys, Nil)
        | q -> q
        )
      ;;

      let enqueue = fun (Q (xs, ys)) y -> norm (Q (xs, Cons (y, ys)));;
      let dequeue = fun q ->
        match q with
        ( Q (Cons (x, xs), ys) -> norm (Q (xs, ys))
        | _ -> raise_empty ()
        )
      ;;

      let hd = fun q ->
        match q with
        ( Q (Cons (x, _), _) -> x
        | _ -> raise_empty ()
        )
      ;;

      let bfs =
        fix (fun bfs q ->
          if is_empty q then Nil
          else
            match hd q with
            ( Lf -> bfs (dequeue q)
            | Br (l, x, r) ->
              Cons (x, bfs (enqueue (enqueue (dequeue q) l) r) )
            )
        )
      ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    include_fix
    ^ include_list
    ^ {|
      type 'a seq =
        | Seq_nil
        | Seq_cons of 'a * (unit -> 'a seq)
      ;;

      external raise_empty : 'a. unit -> 'a;;

      let hd = fun t ->
        match t with
        ( Seq_cons (x, _) -> x
        | _ -> raise_empty ()
        )
      ;;

      let tl = fun t ->
        match t with
        ( Seq_cons (_, tf) -> tf ()
        | _ -> raise_empty ()
        )
      ;;

      let empty = Seq_nil ;;

      let is_empty = fun t ->
        match t with
        ( Seq_nil -> true
        | _ -> false
        )
      ;;

      let map =
        fix (fun map f t ->
          match t with
          ( Seq_nil -> Seq_nil
          | Seq_cons (x, tf) -> Seq_cons (f x, fun () -> map f (tf ()))
          )
        )
      ;;

      let filter =
        fix (fun filter f t ->
          match t with
          ( Seq_nil -> Seq_nil
          | Seq_cons (x, tf) ->
              if f x then
                Seq_cons (x, fun () -> filter f (tf ()))
              else
                filter f (tf ())
          )
        )
      ;;

      let append =
        fix (fun append t1 t2 ->
          match t1 with
          ( Seq_nil -> t2
          | Seq_cons (x, t1f) ->
              Seq_cons (x, fun () -> append (t1f ()) t2)
          )
        )
      ;;

      let interleave =
        fix (fun interleave t1 t2 ->
          match t1 with
          ( Seq_nil -> t2
          | Seq_cons (x, t1f) ->
              Seq_cons (x, fun () -> interleave t2 (t1f ()))
          )
        )
      ;;

      let binary_string =
        fix (fun binary_string bits ->
          Seq_cons (bits, fun () ->
            interleave
              (binary_string (Cons (0, bits)))
              (binary_string (Cons (1, bits))))
        )
      ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    {|
      let id = fun x -> y ;;
    |}
  in
  type_check_and_print str;
  [%expect
    {|
    error[E004]: cannot find value `y` in this scope
        ┌─ expect_test.ml:2:25
      2 │        let id = fun x -> y ;;
        │                          ^ not found in this scope
    |}]
;;

let%expect_test "" =
  let str =
    {|
      (* val id : ('a -> 'a as 'a) -> 'a -> 'a *)
      let id = exists (type 'a) ->
        (fun x -> x : 'a -> 'a -> 'a)
      ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    {|
      let app_error = fun x ->
        (x, x) (fun y -> y)
      ;;
    |}
  in
  type_check_and_print str;
  [%expect
    {|
    error[E011]: mismatched type
        ┌─ expect_test.ml:3:9
      3 │          (x, x) (fun y -> y)
        │          ^^^^^^ `'a -> 'b`
        │                   is not equal to
        │                 `'c * 'd`
    |}]
;;

let%expect_test "" =
  let str =
    {|
      let x =
        (fun y z -> y z) ()
      ;;
    |}
  in
  type_check_and_print str;
  [%expect
    {|
    error[E011]: mismatched type
        ┌─ expect_test.ml:3:26
      3 │          (fun y z -> y z) ()
        │                           ^^ `'a -> 'b`
        │                                is not equal to
        │                              `unit`
    |}]
;;

let%expect_test "" =
  let str =
    {|
      type t =
        | A
      ;;

      type u =
        | A
      ;;

      let x = (A : t) ;;
      let y = (A : u) ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    {|
      type t =
        | A
      ;;

      type u =
        | A
      ;;

      let z = A ;;
  |}
  in
  type_check_and_print str;
  [%expect
    {|
    error[E010]: ambiguous constructor
        ┌─ expect_test.ml:10:15
     10 │        let z = A ;;
        │                ^
        = hint: add a type annotation
    |}];
  type_check_and_print ~defaulting:Scc str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    {|
      type r =
        | K of int
      ;;

      type s =
        | K of int
      ;;

      let a =
        fun old ->
          let g = fun x -> 1 + old (K x) in
          (g 0, (old : r -> int))
      ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    {|
      type m =
        | L
      ;;

      type n =
        | L
      ;;

      let x1 = (fun (z : m) -> 1) L ;;

      let y1 = fun z -> (z : m -> int) L ;;

      let z1 = (fun z -> match z with (L -> 1)) (L : m) ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    {|
      type m =
        | L
      ;;

      type n =
        | L
      ;;

      let good =
        let f = fun x -> match x with (L -> 1) in
        f (L : m)
      ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    {|
      let id = fun x -> x ;;
    |}
  in
  (* Show that stdlib is not added to generated constraint *)
  type_check_and_print ~with_stdlib:false ~dump_constraint:true str;
  [%expect
    {|
    Generated constraint:
    (With_range
     (Let ((id 4) (name id))
      ((type_vars ((Flexible ((id 0) (name Type.Var)))))
       (in_
        (With_range
         (Exists ((id 1) (name Type.Var))
          (Exists ((id 2) (name Type.Var))
           (Conj
            (Eq (Var ((id 0) (name Type.Var)))
             (Arrow (Var ((id 1) (name Type.Var)))
              (Var ((id 2) (name Type.Var)))))
            (With_range
             (Conj True
              (Let ((id 3) (name x))
               ((type_vars ()) (in_ True) (type_ (Var ((id 1) (name Type.Var)))))
               (With_range
                (Instance ((id 3) (name x)) (Var ((id 2) (name Type.Var))))
                ((start 25) (stop 26)
                 (source
                  (Reader
                   ((id 0) (name (expect_test.ml)) (length 34)
                    (unsafe_get <fun>))))))))
             ((start 20) (stop 21)
              (source
               (Reader
                ((id 0) (name (expect_test.ml)) (length 34) (unsafe_get <fun>)))))))))
         ((start 16) (stop 26)
          (source
           (Reader
            ((id 0) (name (expect_test.ml)) (length 34) (unsafe_get <fun>)))))))
       (type_ (Var ((id 0) (name Type.Var)))))
      True)
     ((start 7) (stop 26)
      (source
       (Reader ((id 0) (name (expect_test.ml)) (length 34) (unsafe_get <fun>))))))
    Well typed :)
    |}]
;;

let include_mr_ms_records =
  {|
    type mr = { lbl : int };;
    type ms = { lbl : bool };;
  |}
;;

let%expect_test "" =
  let str =
    {|
      let magic =
        forall (type 'a 'b) ->
          (fun x -> x : 'a -> 'b)
      ;;
    |}
  in
  type_check_and_print str;
  [%expect
    {|
    error[E011]: mismatched type
        ┌─ expect_test.ml:4:21
      4 │            (fun x -> x : 'a -> 'b)
        │                      ^ `'a`
        │                          is not equal to
        │                        `'b`
    |}]
;;

let%expect_test "" =
  let str =
    {|
      let escape = fun f ->
        forall (type 'a) ->
          (f : 'a -> 'a)
      ;;
    |}
    |> Dedent.string
  in
  type_check_and_print str;
  (* NOTE():
     error message looks off due to bug in Grace[^1]

     [1]: https://github.com/johnyob/grace/issues/42 *)
  [%expect
    {|
    error[E012]: generic type variable escapes its scope
        ┌─ expect_test.ml:2:3
      1 │    let escape = fun f ->
      2 │ ╭    forall (type 'a) ->
      3 │ │      (f : 'a -> 'a)
        │ ╰──
      4 │    ;;
    |}]
;;

let%expect_test "" =
  let str =
    {|
      let escape = fun x ->
        forall (type 'a) ->
          (x : 'a)
      ;;
    |}
    |> Dedent.string
  in
  type_check_and_print str;
  (* NOTE():
     error message looks off due to bug in Grace[^1]

     [1]: https://github.com/johnyob/grace/issues/42 *)
  [%expect
    {|
    error[E012]: generic type variable escapes its scope
        ┌─ expect_test.ml:2:3
      1 │    let escape = fun x ->
      2 │ ╭    forall (type 'a) ->
      3 │ │      (x : 'a)
        │ ╰──
      4 │    ;;
    |}]
;;

let%expect_test "" =
  let str =
    {|
      let x =
        (forall (type 'a) -> fun (x : 'a) -> (x : 'a)) ()
      ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    include_mr_ms_records
    ^ {|
      let before_a = ({ lbl = 3 } : mr);;

      let a =
        let x = ({ lbl = 3 } : mr) in
        x.lbl
      ;;

      let after_a =
        let x = ({ lbl = 3 } : mr) in
        ({ lbl = x.lbl } : mr)
      ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    {|
      let x =
        (forall (type 'a) -> ((fun x -> fun y -> y) (fun x -> x) : 'a -> 'a))
      ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    include_mr_ms_records
    ^ include_ref
    ^ {|
      let b =
        let x = (create_ref { lbl = 3 } : mr ref) in
        set_ref x { lbl = 4 }
      ;;

      let c =
        let x = (create_ref { lbl = 3 } : mr ref) in
        (get_ref x).lbl
      ;;

      let f =
        let x = (create_ref { lbl = 3 } : mr ref) in
        (ref_repr x).contents.lbl
      ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    include_mr_ms_records
    ^ include_ref
    ^ {|
      let g = fun (x : mr) ->
        match x with ( { lbl = 1 } -> () )
      ;;

      let h = fun x ->
        match x with (
        | (_ : mr) -> ()
        | { lbl = 1 } -> ()
        )
      ;;

      let i = fun x ->
        match x with (
        | { lbl = 1 } -> ()
        | (_ : mr) -> ()
        )
      ;;

      let l = fun (x : mr ref) ->
        match (ref_repr x) with
        ( { contents = { lbl = 1 } } -> () )
      ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    include_mr_ms_records
    ^ include_ref
    ^ {|
      let m = fun x ->
        match x with
        ( { contents = { lbl = _ } } -> () )
      ;;
    |}
  in
  type_check_and_print str;
  [%expect
    {|
    error[E013]: ambiguous label
        ┌─ expect_test.ml:15:26
     15 │          ( { contents = { lbl = _ } } -> () )
        │                           ^^^
        = hint: add a type annotation
    |}];
  type_check_and_print ~defaulting:Scc str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    include_mr_ms_records
    ^ include_ref
    ^ {|
      let n = fun x ->
        match x with
        ( (_ : mr ref_repr) -> ()
        | { contents = { lbl = _ } } -> ()
        )
      ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    include_mr_ms_records
    ^ include_ref
    ^ {|
      let o = fun x ->
        match x with
        ( (_ : mr ref_repr) -> ()
        | { contents = { lbl = _ } } -> ()
        )
      ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    include_mr_ms_records
    ^ include_ref
    ^ {|
      let r = fun arg ->
        match arg with ( (x : mr ref) -> (get_ref x).lbl )
      ;;

      let s = fun arg ->
        match arg with (
          (x : mr ref) -> set_ref x { lbl = 4 }
        )
      ;;

      let t = fun arg ->
        match (ref_repr arg) with
        ( ({ contents = { lbl = _ } } : mr ref_repr) ->
            set_ref arg { lbl = 4 }
        )
      ;;

      let u = fun arg ->
        match (ref_repr arg) with
        ( ({ contents = { lbl = _ } } : mr ref_repr) ->
            (get_ref arg).lbl
        )
      ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let include_mr_ms_constrs =
  {|
    type mr =
      | A
      | B
    ;;

    type ms =
      | A
      | B
    ;;
  |}
;;

let%expect_test "" =
  let str =
    include_mr_ms_constrs
    ^ include_ref
    ^ {|
      let before_a = (A : mr);;

      let a =
        let x = (A : mr) in
        x
      ;;

      let b =
        let x = (create_ref A : mr ref) in
        set_ref x B
      ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    include_mr_ms_constrs
    ^ include_ref
    ^ {|
      let g = fun (x : mr) ->
        match x with
        ( A -> ()
        | B -> ()
        )
      ;;

      let h = fun x ->
        match x with
        ( (A : mr) -> ()
        | B -> ()
        )
      ;;

      let i = fun x ->
        match x with
        ( A -> ()
        | (B : mr) -> ()
        )
      ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    include_mr_ms_constrs
    ^ include_ref
    ^ {|
      let l = fun (x : mr ref) ->
        match (ref_repr x) with
        ( { contents = A } -> ()
        | { contents = B } -> ()
        )
      ;;
  |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    include_mr_ms_constrs
    ^ include_ref
    ^ {|
      let m = fun x ->
        match (ref_repr x) with
        ( { contents = A } -> ()
        | { contents = B } -> ()
        )
      ;;
    |}
  in
  type_check_and_print str;
  [%expect
    {|
    error[E010]: ambiguous constructor
        ┌─ expect_test.ml:23:24
     23 │          | { contents = B } -> ()
        │                         ^
        = hint: add a type annotation

    error[E010]: ambiguous constructor
        ┌─ expect_test.ml:22:24
     22 │          ( { contents = A } -> ()
        │                         ^
        = hint: add a type annotation
    |}];
  type_check_and_print ~defaulting:Scc str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    include_mr_ms_constrs
    ^ include_ref
    ^ {|
      let n = fun x ->
        match (ref_repr x) with
        ( (_ : mr ref_repr) -> ()
        | { contents = A } -> ()
        )
      ;;

      let o = fun x ->
        match (ref_repr x) with
        ( (_ : mr ref_repr) -> ()
        | { contents = A } -> ()
        )
      ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    include_mr_ms_constrs
    ^ include_ref
    ^ {|
      let s = fun arg ->
        match arg with
        ( (_ : mr ref) -> set_ref arg A )
      ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    include_mr_ms_constrs
    ^ include_ref
    ^ {|
      let t = fun arg ->
        match (ref_repr arg) with
        ( ({ contents = A } : mr ref_repr) ->
            set_ref arg B
        )
      ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    {|
      let xs = (1, 2, 3);;

      let x3 = xs.3;;

      let x3' = (1, 2, 3).3;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    {|
      let f = fun x ->
        let result = x.2 in
        let useless = (x : int * int) in
        result
      ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    {|
      let g =
        let f = fun x -> x.1 in
        f (1, 2)
      ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    {|
      let id = fun x -> x ;; 
      let pid = [ id : 'a. 'a -> 'a ] ;; 
      let see_pid = (fun x -> (@[x], @[x])) pid ;;
      let see_pid_type = forall (type 'a 'b) -> (see_pid : ('a -> 'a) * ('b -> 'b)) ;; 
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    {|
      let id = fun x -> x ;; 
      let pid1 = exists (type 'b) -> [ id : 'a. 'a * 'b -> 'a * 'b ] ;;
      let see_pid1 = (fun x -> (@[x], @[x])) pid1 ;;
      let see_pid1_type = forall (type 'a 'b 'c) -> (see_pid1 : ('a * 'b -> 'a * 'b) * ('c * 'b -> 'c * 'b)) ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    {|
      let id = fun x -> x ;; 
      let pid1 = exists (type 'b) -> [ id : 'a. 'a * 'b -> 'a * 'b ] ;;
      let see_pid1 = (fun x -> (@[x], @[x])) pid1 ;;
      let see_pid1_type_wrong = forall (type 'a 'b 'c 'd) -> (see_pid1 : ('a * 'b -> 'a * 'b) * ('c * 'd -> 'c * 'd)) ;;
    |}
  in
  (* Bug in grace, this should print aligned with the ^^^^^^ *)
  type_check_and_print str;
  [%expect
    {|
    error[E011]: mismatched type
        ┌─ expect_test.ml:5:63
      5 │        let see_pid1_type_wrong = forall (type 'a 'b 'c 'd) -> (see_pid1 : ('a * 'b -> 'a * 'b) * ('c * 'd -> 'c * 'd)) ;;
        │                                                                ^^^^^^^^
        │  `('a * 'b -> 'a * 'b) * ('c * 'b -> 'c * 'b)`
        │    is not equal to
        │  `('d * 'e -> 'd * 'e) * ('f * 'g -> 'f * 'g)`
    |}]
;;

let%expect_test "" =
  let str =
    {|
      let id = fun x -> x ;; 
      let pid2 = [ id : 'a 'b. 'a * 'b -> 'a * 'b ] ;;
      let see_pid2 = (fun x -> (@[x], @[x])) pid2 ;;
      let see_pid2_type = forall (type 'a 'b 'c 'd) -> (see_pid2 : ('a * 'b -> 'a * 'b) * ('c * 'd -> 'c * 'd)) ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    {|
      external combine : 'a. 'a -> 'a -> 'a;;

      let id = fun x -> x ;; 
      let qid = [ id ] ;; 
      let pid = [ id : 'a. 'a -> 'a ] ;;
      let pqid = combine pid qid ;;
      let pqid_type = (pqid : [ 'a. 'a -> 'a ]) ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    {|
      let id = fun x -> x ;;
      let mono_use_pid = fun pid -> @[pid] ;;
      let succ = fun x -> x + 1 ;;
      let mono_use_pid_app_succ = fun pid -> @[pid] succ ;;
    |}
  in
  type_check_and_print str;
  [%expect
    {|
    error[E016]: unknown polytype
        ┌─ expect_test.ml:5:48
      5 │        let mono_use_pid_app_succ = fun pid -> @[pid] succ ;;
        │                                                 ^^^
        = hint: add a type annotation

    error[E016]: unknown polytype
        ┌─ expect_test.ml:3:39
      3 │        let mono_use_pid = fun pid -> @[pid] ;;
        │                                        ^^^
        = hint: add a type annotation
    |}];
  type_check_and_print ~defaulting:Scc str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    {|
      let id = fun x -> x ;;
      let pid = [ id : 'a. 'a -> 'a ] ;; 
      let use_id_pid = (fun pid -> @[pid]) pid ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    {|
      let id = fun x -> x ;;
      let pid = [ id : 'a. 'a ] ;;
    |}
  in
  type_check_and_print str;
  [%expect
    {|
    error[E011]: mismatched type
        ┌─ expect_test.ml:3:19
      3 │        let pid = [ id : 'a. 'a ] ;;
        │                    ^^ `'a -> 'a`
        │                         is not equal to
        │                       `'b`
    |}]
;;

let%expect_test "" =
  let str =
    {|
      let succ = fun x -> x + 1 ;;
      let pid = [ succ : 'a. 'a -> 'a ] ;;  
    |}
  in
  type_check_and_print str;
  [%expect
    {|
    error[E011]: mismatched type
        ┌─ expect_test.ml:3:19
      3 │        let pid = [ succ : 'a. 'a -> 'a ] ;;
        │                    ^^^^ `int -> int`
        │                           is not equal to
        │                         `'a -> 'a`
    |}]
;;

let%expect_test "" =
  let str =
    {|
      let id = fun x -> x ;;
      let pid = [ id : 'a. 'a -> 'a ] ;;
      let use_id_twice_app_pid = (fun pid -> let x = @[pid] in (x, x)) pid ;;
      let xx_pid = (fun pid -> @[pid] @[pid]) pid ;;
      let idide = fun (pid : ['a. 'a -> 'a]) -> let id = @[pid] in (id, id) ;;
      let idide_pid = idide pid ;;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    {|
      let id = fun x -> x ;;
      let use_poly_mono = fun x -> let y = [ (id, x) ] in @[y] ;;
    |}
  in
  type_check_and_print str;
  [%expect
    {|
    error[E016]: unknown polytype
        ┌─ expect_test.ml:3:61
      3 │        let use_poly_mono = fun x -> let y = [ (id, x) ] in @[y] ;;
        │                                                              ^
        = hint: add a type annotation

    error[E016]: unknown polytype
        ┌─ expect_test.ml:3:46
      3 │        let use_poly_mono = fun x -> let y = [ (id, x) ] in @[y] ;;
        │                                               ^^^^^^^
        = hint: add a type annotation
    |}];
  type_check_and_print ~defaulting:Scc str;
  [%expect {| Well typed :) |}]
;;

let%expect_test "" =
  let str =
    {|
      let id = fun x -> x ;;
      let use_poly_mono = fun x -> let y = [ (id, x) ] in 0 ;;
    |}
  in
  type_check_and_print str;
  [%expect
    {|
    error[E016]: unknown polytype
        ┌─ expect_test.ml:3:46
      3 │        let use_poly_mono = fun x -> let y = [ (id, x) ] in 0 ;;
        │                                               ^^^^^^^
        = hint: add a type annotation
    |}]
;;

let%expect_test "" =
  (* All examples from https://dl.acm.org/doi/pdf/10.1145/3408971 *)
  (* The following compares results from QuickLook. We 
     primarily use the manual encoding of OCaml's polyparams into polytypes: 
       [[ fun x -> e ]] = fun x -> let x = @[ x ] in [[ e ]]
       [[ e1 e2 ]] = [[ e1 ]] [ [[ e2 ]] ] *)
  (* Some special attention is given to higher-rank poly that doesn't 
     fit into polyparams -- e.g. E1b *)
  let str =
    include_list
    ^ {|
      external head : 'a. 'a list -> 'a;;
      external tail : 'a. 'a list -> 'a list;;
      external single : 'a. 'a -> 'a list;;
      external concat : 'a. 'a list -> 'a list -> 'a list;;
      external length : 'a. 'a list -> int;;
      external id : 'a. 'a -> 'a;;
      external ids : [ 'a. 'a -> 'a ] list;;
      external map : 'a 'b. ('a -> 'b) -> 'a list -> 'b list;;
      external app : 'a 'b. ('a -> 'b) -> 'a -> 'b;;
      external revapp : 'a 'b. 'a -> ('a -> 'b) -> 'b;;
      external poly : [ 'a. 'a -> 'a ] -> int * bool;;
      external inc : int -> int;;
      external incs : (int -> int) list;;
      external choose : 'a. 'a -> 'a -> 'a;;
      external auto : [ 'a. 'a -> 'a ] -> [ 'a. 'a -> 'a ];;
      external auto2 : 'b. [ 'a. 'a -> 'a ] -> 'b -> 'b;;
      external compose : 'a 'b 'c. ('b -> 'c) -> ('a -> 'b) -> 'a -> 'c;;

      let const2 = fun x y -> x;;

      let a1 = const2;;
      let a2 = choose id;; 
      let a3 = choose Nil ids;;
      let a4 = fun x -> 
        let x = @[(x : [ 'a. 'a -> 'a ])] in 
        x x 
      ;;
      let a5 = id auto;; 
      let a6 = id auto2;; 
      let a7 = choose id auto;; 

      (* The following is ill-typed *)
      (* let a8 = choose id auto2;; *)
      (* with error:
         error[E011]: mismatched type
            ┌─ expect_test.ml:32:26
         32 │        let a8 = choose id auto2;;
            │                           ^^^^^ `(ν. 'a. 'a -> 'a) -> 'a -> 'a`
            │                                   is not equal to
            │                                 `'b -> 'b`
      *)

      (* The following is ill-typed *)
      (* external f : 'a. ('a -> 'a) -> 'a list -> 'a;; *)
      (* let a9 = f (choose id) ids ;; *)
      (* with error: 
         error[E011]: mismatched type
             ┌─ expect_test.ml:43:30
          43 │        let a9 = f (choose id) ids ;;
             │                               ^^^ `((ν. 'a. 'a -> 'a)) list`
             │                                     is not equal to
             │                                   `('a -> 'a) list`
      *)
     
      let a10 = 
        (poly [id], poly [fun x -> x], id poly [fun x -> x]);;

      external k : 'a. 'a -> 'a list -> int;; 
      external xs : ([ 'a. 'a -> 'a ] -> int * bool ) list;; 
      let a11 = k (fun f -> let f = @[f] in (f 1, f true)) xs;;

      let a12 = 
        (poly [id], app poly [id], revapp [id] poly);;

      (* single id will have the monotype [('a -> 'a) list] *)
      let b1 = 
        (length ids, tail ids, head ids, single id)
      ;;

      let b2 = 
        Cons ([ id ], ids)
      ;;

      let b3 = 
        Cons ([ fun x -> x ], ids)
      ;;

      let b4 = 
        concat (single inc) (single id) 
      ;;

      let b5 = 
        map poly (single [ id ])
      ;;

      let b7 = 
        (map head (single ids), @[head ids] true)
      ;;
      
      (* Some more examples from the GI paper *)
      let b1 = 
        fun f -> 
          let f = @[( f : [ 'a. 'a -> 'a ])] in 
          (f 1, f true)
      ;;

      (* This is quite interesting, no other system typechecks this *)
      let b2 = 
        fun xs -> 
          poly (head xs)
      ;;

      let c1b = 
        fun (f : [ 'a. 'a -> 'a ]) -> 
          let f = @[f] in 
          (f 1, f true) 
      ;;

      type char;;
      external g : ([ 'a. 'a -> 'a ] -> int * bool) -> char;;
      let c1c = 
        g (fun f ->
          let f = @[f] in 
          (f 1, f true))
      ;;

      (* This is ill-typed ( we don't have an encoding for arbitrary-rank polymorphism ) *)
      (* external r : [ 'a. 'a -> [ 'b. 'b -> 'b ] ] -> int;; *)
      (* let c2 = r [ fun x -> fun y -> y ] ;; *)
      (* with error:
         error[E011]: mismatched type
             ┌─ expect_test.ml:83:11
          83 │          r [ fun x -> fun y -> y ]
             │            ^^^^^^^^^^^^^^^^^^^^^^^ `'a -> 'b -> 'b`
             │                                      is not equal to
             │                                    `'c -> (ν. 'a. 'a -> 'a)`
      *)

      external k : 'a. 'a -> 'a list -> 'a;;
      external h : int -> [ 'a. 'a -> 'a ];;
      external lst : [ 'a. int -> 'a -> 'a ] list;;

      (* This is ill-typed *)
      (* let e1a = k h lst;; *)

      let e1b = k [ (fun x -> @[ h x ]) ] lst;;

      let e2a = fun x -> poly x;;

      let e2b = (fun x -> poly x : [ 'a. 'a -> 'a ] -> int * bool);;

      let e3a = app poly [ id ];;

      let e3b = app (fun x -> poly x) [ id ];;

      let e4a = map poly ids;;

      let e4b = map (fun x -> poly x) ids;;

      let e5a = compose poly head;;

      let e5b = fun xs -> poly (head xs);;
    |}
  in
  type_check_and_print str;
  [%expect {| Well typed :) |}]
;;
